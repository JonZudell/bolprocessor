// Bol Processor version BP2.8.1
// Data file saved as '-da.checkArticulation'. Date: Mar 14 Avr 1998 -- 17:23
-or.checkArticulation
-se.checkArticulation
 
// -da.checkArticulation
// These are examples of articulations (legato and staccato) that modify the durations of sound-objects in sequences.
// These examples are not meant to sound "nice"! They have been designed to make things clear on both sounds and graphics.
// Also for the sake of clarity I have used sound-objects that contain one single note.  You should keep in mind that articulation modifies sound-object durations. If a sound-object contains several NoteOn/NoteOff pairs, the corresponding notes will not be concerned by legato or staccato. The underlying philosophy is that a sound–object is a sequence of MIDI codes whose order is immuable.  A note is a particular case of sound–object containing a single NoteOn/NoteOff pair.

// Basic example
do4 re4 mi4 fa4 _legato(20) do4 re4 mi4 fa4 _staccato(20) do4 re4 mi4 fa4

// Here, legato is not effective on the "do4 do4" sequence because a key
// can't be pushed before it has been released.  BP2 takes care of not throwing two consecutive
// NoteOn's.
// Also notice that the last "fa4" is not extended.
do4 re4 do4 do4 fa4 _legato(30) do4 re4 do4 do4 fa4 {2,mi4 re4 do4 re4} do4 do4 fa4

// Stepwise change of legato from 0 to 90 and from 90 down to 10.  There is no change after
// _legato(10) because there is no further value.
/2  _articulstep _legato(0) do4 re4 mi4 fa4 sol4 la4 si4 _legato(90) do5 si4 la4 sol4 fa4 mi4 re4 do4 si3 la3 si3 _legato(20) do4 si3 do4
/2  _articulstep _legato(0) do4_ mi4_ sol4_ si4 _legato(90) do5 si4 la4 sol4 fa4 mi4 re4 do4 si3 la3 si3 _legato(20) do4 si3 do4

// Stepping starts here on the first occurrence of 'fa4'
/2 _legato(0)  do4 re4 mi4 _articulstep fa4 sol4 la4 si4 _legato(90) do5 si4 la4 sol4 _legato(20) do4 si3 do4

// Exactly the same piece because _articulcont is identical to _articulstep.
/2 _legato(0)  do4 re4 mi4 _articulcont fa4 sol4 la4 si4 _legato(90) do5 si4 la4 sol4 _legato(20) do4 si3 do4

// Here, stepwise change of legato from 0 to 90 and from 90 down to a value (40) that is between 20 and 90, because _articulfixed stops the stepping.
/2 _articulstep _legato(0) do4 re4 mi4 fa4 sol4 la4 si4 _legato(90) do5 si4 la4 sol4 fa4    mi4 re4 do4 _articulfixed si3 la3 si3 _legato(20) do4 si3 do4

// This example shows that staccato is simply a negative legato, and vice versa.
/2  _articulstep _staccato(80) do4 re4 mi4 fa4 sol4 la4 si4 do5 si4 la4 sol4 _legato(60) fa4 mi4 re4 do4 si3 la3 si3 _legato(0) do4 si3 _staccato(80) do4 re4 mi4 fa4

// Unless otherwise indicated, articulation is applied similarily to all fields of a polymetric structure.
/2 _articulstep _staccato(80) do4 re4 mi4 fa4 sol4 {la4 si4 do5 si4 la4 sol4 fa4,mi5 sol5 mi5 do5 -} mi4 re4 do4 si3 la3 si3 _legato(60) do4 si3 do4 re4 mi4 fa4

// Here, the second field "mi5 sol5 mi5 do5 -" remains at legato(20). Note again that the final objects of the sequence (do4) is not extended.
/2 _articulstep _staccato(100) do4 re4 mi4 fa4 sol4 {la4 si4 do5 si4 la4 sol4 fa4,_legato(20) mi5 sol5 mi5 do5 -} mi4 re4 do4 si3 la3 si3 _legato(50) do4 si3 do4

// Here, the second field "re5 mi5 do5 do5 la4 sol4" varies independently from legato(60) down to staccato(90).
/2 _articulstep _staccato(80)  do4 re4 mi4 fa4 sol4 {la4 _ _ do5 _ _ fa4 _ _,_legato(60) re5 mi5 do5 do5 la4 sol4 _staccato(90)} mi4 re4 do4 si3 la3 si3 _legato(100) do4 si3 do4

//----------------------------------+
