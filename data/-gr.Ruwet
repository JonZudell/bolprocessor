// Bol Processor version BP2.7.2
// Grammar file saved as '-gr.Ruwet'. Date: Dim 29 Juin 1997 -- 22:01
-ho.Ruwet
-se.Ruwet

// This model of theme-and-variation was inspired by an analysis by Ruwet.  It was done by a student of mine using a very old version of BP2.  I have "recycled" it recently.  Suggestions are welcome.

// A mistake in durations has been fixed on 29/6/97

// Note that the alphabet is using three homomorphisms: "m1", "m2", "mineur"... defined on simple notes.

ORD [1]
GRAM#1[1] S   --> 4+4+4+4+4+4/4 P1 P2 P3
GRAM#1[2] P1 --> A8 B"8 C8 B'8
GRAM#1[3] P2 --> A8 B8 C8 B8
GRAM#1[4] P3 --> C'8 B'8 C'8 B'8
GRAM#1[5] A8 --> |a4| |b4|
GRAM#1[6] B"8 --> |c"4| |f4| 
GRAM#1[7] B8 --> |c4| |f4|
GRAM#1[8] B'8 --> |c'4|Ê|f4| 
GRAM#1[9] C8 --> |d4| |e4|
GRAM#1[10] C'8 --> (= |e4|)(: |e4|)
-------------------------------
LIN [2]
GRAM#2[1] |a4| --> |x| |z31|
GRAM#2[2] |b4| --> |y| |z5|
GRAM#2[3] |c4| --> |w| |z31|
GRAM#2[4] |c"4|  --> |w|Ê|z311|
GRAM#2[5] |c'4| --> |w| |z3| 
GRAM#2[6] |d4| --> |miny| |z32|
GRAM#2[7] |e4| --> |y1|Ê|z31|
GRAM#2[8] |f4| --> |o|Ê|z1|
--------------------------------
LIN [3]
GRAM#3[1] <4>|x| --> fa4 do5
GRAM#3[2] <1>|x| --> |x1| [Variant]
GRAM#3[3] <1>|x| --> |x2| [Variant]
GRAM#3[4] <1>|x| --> |x3| [Variant]
GRAM#3[5] <1>|x| --> |x4| [Variant]


GRAM#3[6] |y| --> fa4 la4 
GRAM#3[7] |y| --> |y1| [Variant]
GRAM#3[8] |y| --> |y2| [Variant]
GRAM#3[9] |y| --> |y3| [Variant]
GRAM#3[10] |y| --> |y4| [Variant]
GRAM#3[11] |y| --> |y5| [Variant]
GRAM#3[12] |y| --> |y6| [Variant]
GRAM#3[13] |y| --> |y7| [Variant]
GRAM#3[14] |y| --> |y8| [Variant]


GRAM#3[15] |w| --> la4 do5
GRAM#3[16] |w| --> |w1| [Variant]
GRAM#3[17] |w| --> |w2| [Variant]
GRAM#3[18] |w| --> |w3| [Variant]
GRAM#3[19] |w| --> |w4| [Variant]
GRAM#3[20] |w| --> |w5| [Variant]


GRAM#3[21] <4> |z1| --> fa4 fa4
GRAM#3[22] <1> |z1| |miny| --> |z12| |miny| [Variant]
GRAM#3[23] <1> |z1| |miny| --> |z13| |miny| [Variant]


GRAM#3[24] |z3| --> la4 la4
GRAM#3[25] |z3| --> |z31| [Variant]
GRAM#3[26] |z3| --> |z311| [Variant]
GRAM#3[27] |z3| --> |z32| [Variant]
GRAM#3[28] |z3| --> |z321| [Variant]
GRAM#3[29] |z3| --> |z33| [Variant]
GRAM#3[30] |z3| --> |z34| [Variant]
GRAM#3[31] |z3| --> |z35| [Variant]
GRAM#3[32] |z3| --> |z36| [Variant]

GRAM#3[33] |z5| --> do5 do5
GRAM#3[34] |z5| --> |z51| [Variant]
GRAM#3[35] |z5| --> |z511| [Variant]
GRAM#3[36] |z5| --> |z52| [Variant]
GRAM#3[37] |z5| --> |z521| [Variant]
GRAM#3[38] |z5| --> |z53| [Variant]
GRAM#3[39] |z5| --> |z54| [Variant]
GRAM#3[40] |z5| --> |z55| [Variant]
GRAM#3[41] |z5| --> |z56| [Variant]
GRAM#3[42] |z5| --> |z57| [Variant]


GRAM#3[43] |z31| --> sib4 la4
GRAM#3[44] |z32| --> sol4 la4
GRAM#3[45] |z311| --> sib4 {1,la4 la4}

GRAM#3[46] <3> |o| --> sib4 {1,la4 sol4}
GRAM#3[47] <1> |o| |miny| --> |o1|Ê|miny| [Variant]
GRAM#3[48] <1> |o| |miny| --> |o2|Ê|miny| [Variant]
GRAM#3[49] <1> |o| (|z13|) --> |o3|Ê [Variant.  Remote context ()]
--------------------------------
ORD [4]
GRAM#4[1] |x1| --> {1, fa4 la4} do5
GRAM#4[2] |x2| --> fa4 {1, la4 do5} 
GRAM#4[3] |x3| --> {2, fa4 sol4 la4 do5}  [?]
GRAM#4[4] |x4| --> {2, fa4 la4 sib4 do5}  [?]

GRAM#4[5] |y1| --> fa4 {1,sol4 la4}
GRAM#4[6] |y2| -->{1,fa4 sol4} la4 [Variant][bug fixed here]
GRAM#4[7] |y3| --> fa4 {1,la4 sib4} [Variant]
GRAM#4[8] |y4| --> {2,fa4 sol4 la4 sib4}[?] [Variant]
GRAM#4[9] |y5| -->  {2,fa4 la4 sol4 la4}[?] [Variant]
GRAM#4[10] |y6| --> {2,fa4 sol4 sib4 la4}[?] [Variant]
GRAM#4[11] |y7| --> {2,fa4 la4 sib4 la4}[?] [Variant]
GRAM#4[12] |y8| --> {2,fa4 sol4 fa4 la4}[?] [Variant]

GRAM#4[13] |w1| -->{1, la4 sib4} do5 [Variant]
GRAM#4[14] |w2| --> la4 {1, sib4 do5} [Variant]
GRAM#4[15] |w3| --> {2,la4 sib4 do5 sib4}[?] [Variant]
GRAM#4[16] |w4| --> {2,la4 sol4 la4 do5}[?] [Variant]
GRAM#4[17] |w5| --> {2,la4 sib4 la4 do5}[?] [Variant]


GRAM#4[18] |z11| --> {1,fa4 mi4} fa4 [Not relevant]
GRAM#4[19] |z12| --> {1,fa4 sol4} fa4
GRAM#4[20] |z13| --> sol4 fa4 [|z13| is used as context in the preceding grammar]
GRAM#4[21] |z14| --> mi4 fa4  [Not relevant]

GRAM#4[22] |z31| --> sib4 la4
GRAM#4[23] |z311| --> sib4 {1,la4 la4}
GRAM#4[24] |z32| --> sol4 la4 [Variant]
GRAM#4[25] |z321| --> sol4 {1,la4 la4} [Variant]
GRAM#4[26] |z33| -->{1, la4 sol4} la4  [Variant]
GRAM#4[27] |z34| -->{1, la4 sib4} la4  [Variant]
GRAM#4[28] |z35| --> {2,la4 sol4 sib4 la4}[Variant]   [?] 
GRAM#4[29] |z36| --> {2,sib4 la4 sol4 la4} [Variant]   [?]

GRAM#4[30] |z51| --> re5 do5 [Variant]
GRAM#4[31] |z511| --> re5 {1, do5 do5 } [Variant]
GRAM#4[32] |z52| --> sib4 do5 [Variant]
GRAM#4[33] |z521| --> sib4 {1, do5 do5 } [Variant]
GRAM#4[34] |z53| --> {1, do5 sib4} do5 [Variant]
GRAM#4[35] |z54| --> {1, do5 re5} do5 [Variant]
GRAM#4[36] |z55| --> {2,do5 sib4 re5 do5}[Variant]   [?] 
GRAM#4[37] |z56| --> {2,re5 do5 sib4 do5}[Variant]   [?] 
GRAM#4[38] |z57| --> {2,do5 re5 do5 sib4}[Variant]   [?] 


GRAM#4[39] |o1| --> {2,do5 sib4 la4 sol4}
GRAM#4[40] |o2| --> {2,sib4 sib4 la4 sol4}
GRAM#4[41] |o3| --> {2,sib4 do5 sib4 la4}

GRAM#4[42] |miny| --> mineur (= |y|)
------------------------------
ORD [5]
GRAM#5[1] |y| --> fa4 la4
