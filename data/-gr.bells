V.2.5
Date: Tue, Jun 20, 1995 -- 15:20
-ho.cloches1
-se.cloches

RND
GRAM#1[1] S --> /4 Tune
GRAM#1[2] Tune --> Canonic   New   B A D C   X1234	 End
GRAM#1[3] Tune --> Canonic   New   B A C D   X12	 End
GRAM#1[4] Tune --> Canonic   New   A C B D   X23	 End
GRAM#1[5] Tune --> Canonic   New   A B D C   X34  	 End
----------------------------------------------------------
LIN
GRAM#2[1] <°> A B C D ?1 End  -->   A B C D [Infinite weight]

 GRAM#2[2] #(?1 ?3 ?2 ?4) ?1 ?2 ?3 ?4  X1234 --> ?1 ?2 ?3 ?4   New   ?1 ?3 ?2 ?4 	X23
 GRAM#2[3] #(?2 ?1 ?4 ?3) ?1 ?2 ?3 ?4  X23 --> ?1 ?2 ?3 ?4   New   ?2 ?1 ?4 ?3 	X1234
 GRAM#2[4] #(?1 ?2 ?4 ?3) ?1 ?2 ?3 ?4  X12 --> ?1 ?2 ?3 ?4   New   ?1 ?2 ?4 ?3 	X34
 GRAM#2[5] #(?2 ?1 ?4 ?3) ?1 ?2 ?3 ?4  X12 --> ?1 ?2 ?3 ?4   New   ?2 ?1 ?4 ?3 	X1234
 GRAM#2[6] #(?2 ?1 ?4 ?3) ?1 ?2 ?3 ?4  X34 --> ?1 ?2 ?3 ?4   New   ?2 ?1 ?4 ?3 	X1234
 GRAM#2[7] #(?2 ?1 ?3 ?4) ?1 ?2 ?3 ?4  X34 --> ?1 ?2 ?3 ?4   New   ?2 ?1 ?3 ?4 	X12
 GRAM#2[8] #(?2 ?1 ?3 ?4) ?1 ?2 ?3 ?4  X1234 --> ?1 ?2 ?3 ?4   New   ?2 ?1 ?3 ?4 	X12
 GRAM#2[9] #(?1 ?2 ?4 ?3) ?1 ?2 ?3 ?4  X1234 --> ?1 ?2 ?3 ?4   New   ?1 ?2 ?4 ?3 	X34
-----------------------------------------------------------
ORD
[These rules are used in case the infinite-weight rule could not be applied.]

GRAM#3[1] LEFT X12 --> lambda
GRAM#3[2] LEFT X34 --> lambda
GRAM#3[3] LEFT X23 --> lambda
GRAM#3[4] LEFT X1234 --> lambda
GRAM#3[5] LEFT End --> lambda

GRAM#3[6] LEFT New --> lambda

GRAM#3[7] LEFT Canonic --> A B C D
-----------------------------------------------------------
ORD
GRAM#4[1] A B --> do3 B
GRAM#4[2] A #B --> do4 #B
GRAM#4[3] B --> sol3
GRAM#4[4] B --> sol4
GRAM#4[5] C --> re4
GRAM#4[6] C --> re5
GRAM#4[7] D A --> mi4 A
GRAM#4[8] D #A --> mi5 #A

DATA:
C-75 Gamelan



