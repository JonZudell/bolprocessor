// Bol Processor version BP2.9.1
// Grammar file saved as '-gr.dhadhatite'. Date: Mer 8 Juil 1998 -- 1:06
-se.dhadhatite
-ho.dhadhatite

/* "dhadhatite" qa‘ida. This is a beginner's piece containing only
a small number of different strokes (bols). It does not have a wide
scope of variation in terms of stroke combinations. To compensate
this, it offers a wide variety of structural rules (see GRAM#2).
For these reasons, items are very difficult to analyse. There is
notably an ambiguity between the two fixed patterns 'dhadhatitedhadhadheena' and 'dhadhatitedhadhateena' (both notated
'SF') which appear in specific contexts, notably in a 'band'
(closed) section: * (= SF ++ SF). I have used the '+' sign to mark
contexts and decide which variant of the pattern has been found. */

RND
_mm(60.0000) _striated
GRAM#1[1] <100> S <-> 4+4+4+4/4 S64 
GRAM#1[2] <100> S64 <-> S1F S2F S1V S2F E32 
GRAM#1[3] <100> S64 <-> S1V S2F S1F S2F E32 
GRAM#1[4] <100> S64 <-> S1F S2V S1F S2F E32 
GRAM#1[5] <100> S64 <-> S1V S2V S1F S2F E32
------------------------------------------------------
LIN 
GRAM#2[1] <100> S1F S2F S1V S2F E32 <-> S1F +S2F (= V8 ) +S2F * (= S1F ++ S2F ) (: V8 ) S1F 
GRAM#2[2] <100> S1V S2F S1F S2F E32 <-> (=++ A1 V7 ) +S2F S1F +S2F * (:++ A1 V7 ) * (= ++ S2F ) S1F S1F 
GRAM#2[3] <100> S1V S2F S1F S2F E32 <-> (=++ A2 V6 ) +S2F S1F +S2F * (:++ A2 V6 ) * (= ++ S2F ) S1F S1F 
GRAM#2[4] <100> S1F S2V S1F S2F E32 <-> S1F (= V6 + B2 ) S1F +S2F * (= S1F ) * (: V6 + B2 ) S1F S1F 
GRAM#2[5] <100> S1V S2V S1F S2F E32 <-> (=++ A1 V11 + B4 ) S1F +S2F * (:++ A1 V11 + B4 ) S1F S1F 
GRAM#2[6] <100> S1V S2V S1F S2F E32 <-> (=++ A2 V10 + B4 ) S1F +S2F * (:++ A2 V10 + B4 ) S1F S1F 
GRAM#2[7] <100> S1V S2V S1F S2F E32 <-> (=++ A1 V13 + B2 ) S1F +S2F * (:++ A1 V13 + B2 ) S1F S1F 
GRAM#2[8] <100> S1V S2V S1F S2F E32 <-> (=++ A2 V12 + B2 ) S1F +S2F * (:++ A2 V12 + B2 ) S1F S1F 
GRAM#2[9] <100> S1V S2V S1F S2F E32 <-> (=++ A1 V15 ) S1F +S2F * (:++ A1 V15 ) S1F S1F 
GRAM#2[10] <100> S1V S2V S1F S2F E32 <-> (=++ A2 V14 ) S1F +S2F * (:++ A2 V14 ) S1F S1F 
----------------------------------------------------------------
LIN
GRAM#3[1] <100> V2 <-> T2
GRAM#3[2] <10> V15 <-> T1 V14 
GRAM#3[3] <100> V15 <-> T2 V13 
GRAM#3[4] <10> V14 <-> T1 V13 
GRAM#3[5] <100> V14 <-> T2 V12 
GRAM#3[6] <10> V13 <-> T1 V12 
GRAM#3[7] <100> V13 <-> T2 V11 
GRAM#3[8] <10> V12 <-> T1 V11 
GRAM#3[9] <100> V12 <-> T2 V10 
GRAM#3[10] <10> V11 <-> T1 V10 
GRAM#3[11] <100> V11 <-> T2 V9 
GRAM#3[12] <10> V10 <-> T1 V9 
GRAM#3[13] <100> V10 <-> T2 V8 
GRAM#3[14] <10> V9 <-> T1 V8 
GRAM#3[15] <100> V9 <-> T2 V7 
GRAM#3[16] <10> V8 <-> T1 V7 
GRAM#3[17] <100> V8 <-> T2 V6 
GRAM#3[18] <10> V7 <-> T1 V6 
GRAM#3[19] <100> V7 <-> T2 V5 
GRAM#3[20] <10> V6 <-> T1 V5 
GRAM#3[21] <100> V6 <-> T2 V4 
GRAM#3[22] <10> V5 <-> T1 V4 
GRAM#3[23] <100> V5 <-> T2 V3 
GRAM#3[24] <10> V4 <-> T1 V3 
GRAM#3[25] <100> V4 <-> T2 V2 
GRAM#3[26] <10> V3 <-> T1 V2 
GRAM#3[27] <100> V3 <-> T2 T1 
GRAM#3[28] <10> V2 <-> T1 T1 
--------------------------------------
RND
GRAM#4[1] <100> T1 <-> dha
GRAM#4[2] <100> T1 <-> -
GRAM#4[3] <100> T2 <-> dhadha
GRAM#4[4] <100> T2 <-> tite
GRAM#4[5] <50> T2 <-> dheena
GRAM#4[6] <50> T2 <-> teena
---------------------------------------
ORD
GRAM#5[1] <100> + B2 <-> +teena
GRAM#5[2] <100> + B4 <-> +dhadhateena
GRAM#5[3] <100> ++ A1 <-> ++dha
GRAM#5[4] <100> ++ A2 <-> ++dhadha
GRAM#5[5] <50> ++ A2 <-> ++dheena
GRAM#5[6] <100> #+ S1F <-> #+ dhadhatitedhadhadheena
GRAM#5[7] <100> + S2F <-> + dhadhatitedhadhateena
GRAM#5[8] ++ S2F <-- ++ dhadhatitedhadhadheena [This rule must be last]
------------
TEMPLATES:
[1] 4+4+4+4/4 ________+________(@0 ________)+________ * (@0 ________++________)(@2 )________
[2] 4+4+4+4/4 (@0 ++________)+________________+________ * (@1 ) * (@0 ++________)________________
[3] 4+4+4+4/4 ________(@0 ______+__)________+________ * (@0 ________) * (@2 )________________
[4] 4+4+4+4/4 (@0 ++____________+____)________+________ * (@1 )________________
[5] 4+4+4+4/4 (@0 ++______________+__)________+________ * (@1 )________________
[6] 4+4+4+4/4 (@0 ++________________)________+________ * (@1 )________________
------------
