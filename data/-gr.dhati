// Bol Processor version BP2.7.1
// Grammar file saved as '-gr.dhati'. Date: Sam 21 Juin 1997 -- 21:39
-gl.D50
-se.dhati
-ho.dhati

[This is a qa‘ida transfered from BP1 and modified using polymetric… ]
[ …structures]
[Use percussion sounds for bols and tones for notes.]
[e.g. I-46 Try on my D50]
[mm = 15]

INIT: Play I46

RND
GRAM#1[1] <0> S -->  4+4+4+4/4 {16,Qaida}
GRAM#1[2] S -->  4+4+4+4/4 {16,Lahra,Qaida}
GRAM#1[3] S <-- Qaida
GRAM#1[4] <1>  Qaida  <->  (=+ S64 ;)
-------------------------------------------------------------
LIN 
GRAM#2 [1] <500>  S64  <->  L16 + S48
GRAM#2 [2] <500>  S64  <->  L14  S50 
GRAM#2 [3] <100>  S64  <->  L12  S52 
GRAM#2 [4] <100>  S64  <->  L24  S40 
GRAM#2 [5] <500>  S48  <->  M16 + S32
GRAM#2 [6] <100>  S48  <->  M14  S34 
GRAM#2 [7] <100>  S48  <->  M40  O8 
GRAM#2 [8] <500>  S50  <->  M18 + S32 
GRAM#2 [9] <100>  S50  <->  M34 + S16 
GRAM#2 [10] <100>  S50  <->  M18 + * (=+ N14 )  O18 
GRAM#2 [11] <100>  S16  <->  O16 
GRAM#2 [12] <100>  S50  <->  V10  A'8-2  * (= N18 +) + O16
GRAM#2 [13] <100>  S50  <->  M20  * (= N14 +) + O16 
GRAM#2 [14] <100>  S52  <->  V28  A8-2  O18 
GRAM#2 [15] <100>  S52  <->  M20 + S32 
GRAM#2 [16] <100>  S40  <->  M8 + S32 
GRAM#2 [17] <40>  S32  <->  * (=+ N16 +) + S16 
GRAM#2 [18] <100>  S32  <->  * (={8,+ V12} A8 +) + O16 
GRAM#2 [19] <100>  S32  <->  * (={8,+ A16}  V8 +) + O16 
GRAM#2 [20] <100>  S32  <->  * (={16,+ V24 +}) + O16 
GRAM#2 [21] <100>  S32  <->  * (={16,+ N'16 + A16 +}) + O16 
GRAM#2 [22] <40>  S32  <->  * (=+ N14 )  O18 
GRAM#2 [23] <100>  S34  <->  O34 
GRAM#2 [24] <100>  S34  <->  * (= N18 +) + S16 
GRAM#2 [25] <5>  S16  <-> {16,+ A16  O16 ;}
-------------------------------------------------------------
RND 
GRAM#3 [1] <100>  LEFT  M16  <->  V16 
GRAM#3 [2] <100>  LEFT  N18  <->  V18 
GRAM#3 [3] <100>  LEFT  (=+ L16  <->  (=+ A16 
GRAM#3 [4] <100>  LEFT  (=+ L16  <->  (=+ V16 
GRAM#3 [5] <100>  LEFT  (=+ L14  <->  (=+ V10  A'6-2 
GRAM#3 [6] <100>  LEFT  M14  <->  A'16-2 
GRAM#3 [7] <100>  LEFT  (=+ L14  <->  (=+ A16-2 
GRAM#3 [8] <100>  LEFT  (=+ L14  <->  (=+ A'16-2 
GRAM#3 [9] <100>  LEFT  (=+ L12  <->  (=+ A16-4 
GRAM#3 [10] <100>  LEFT  (=+ L24  <->  (=+ V24 
GRAM#3 [11] <100>  LEFT + M16 + * <-> + V10  A'6 + *
GRAM#3 [12] <100>  LEFT + M16  <-> + A'16 
GRAM#3 [13] <100>  LEFT  M8 + * <->  A'8 + *
GRAM#3 [14] <100>  LEFT  M16 + * <->  V8  A'8 + *
GRAM#3 [15] <100>  LEFT  M14  <->  V8  A'8-2 
GRAM#3 [16] <100>  LEFT  M18 + * <->  V10  A'8 + *
GRAM#3 [17] <100>  LEFT  M18 + * <->  V18 + *
GRAM#3 [18] <100>  LEFT  M34 + <->  V28  A'6 +
GRAM#3 [19] <100>  LEFT  M34 + <->  V26  A'8 +
GRAM#3 [20] <100>  LEFT  M20 + * <->  V12  A'8 + *
GRAM#3 [21] <100>  LEFT  M20  <->  V20 
GRAM#3 [22] <100>  LEFT  M40  <->  V8  A'8-2  V26
GRAM#3 [23] <100>  LEFT  * (={16,+ N'16  <->  * (={16,+ V16
GRAM#3 [24] <100>  LEFT  * (={16,+ N'16  <->  * (={16,+ N16
GRAM#3 [25] <100>  LEFT  N16 + <->  V12  A4 +
GRAM#3 [26] <100>  LEFT  N16 + <->  V8  A8 +
GRAM#3 [27] <100>  LEFT  N16 + <->  V10  C6 +
GRAM#3 [28] <100>  LEFT  * (=+ N16  <->  * (=+ A16
GRAM#3 [29] <100>  LEFT  M14  <->  V8  A8-2 
GRAM#3 [30] <100>  LEFT  * (=+ N14  <->  * (=+ V8  A8-2 
GRAM#3 [31] <100>  LEFT  * (=+ N14  <->  * (=+ A16-2 
GRAM#3 [32] <100>  LEFT  N14 + <->  V6  A8 +
GRAM#3 [33] <100>  LEFT  O8 ; <->  A8 ;
GRAM#3 [34] <100>  LEFT  O34 ; <->  V26  A8 ;
GRAM#3 [35] <100>  LEFT  O18 ; <->  V10  A8 ;
GRAM#3 [36] <100>  LEFT  O16 ; <->  V8  A8 ;
GRAM#3 [37] <100>  LEFT + O16 ; <-> + A16 ;
-------------------------------------------------------------
LIN 
GRAM#4 [1] <100>  V30  <->  V  V  V28 
GRAM#4 [2] <100>  V28  <->  V  V  V26 
GRAM#4 [3] <100>  V26  <->  V  V  V24 
GRAM#4 [4] <100>  V24  <->  V  V  V  V  V20 
GRAM#4 [5] <100>  V20  <->  V  V  V18 
GRAM#4 [6] <100>  V18  <->  V  V  V16 
GRAM#4 [7] <100>  V16  <->  V  V  V  V  V12 
GRAM#4 [8] <100>  V12  <->  V  V  V10 
GRAM#4 [9] <100>  V10  <->  V  V  V8 
GRAM#4 [10] <100>  V8  <->  V  V  V6 
GRAM#4 [11] <100>  V6  <->  V  V  V  V  V  V 
-------------------------------------------------------------
LIN 
GRAM#5 [1] <100>  ? V  <->  ? -
GRAM#5 [2] <100>  V  <-> dha
GRAM#5 [3] <100>  V  V  <-> trkt
GRAM#5 [4] <100>  V  V  <-> dheena
GRAM#5 [5] <100>  V  V  <-> teena
GRAM#5 [6] <100>  V  V  <-> dhati
GRAM#5 [7] <100>  V  V  <-> gena
GRAM#5 [8] <100>  V  V  <-> dhage
GRAM#5 [9] <100> + V  V  <-> +tidha
GRAM#5 [10] <100> - V  V  <-> -tidha
GRAM#5 [11] <100> kt V  V  <-> kttidha
GRAM#5 [12] <100> na V  V  <-> natidha
GRAM#5 [13] <100> ge V  V  <-> getidha
GRAM#5 [14] <100> - V  V  <-> -ti-
GRAM#5 [15] <100> kt V  V  <-> ktti-
GRAM#5 [16] <100> ge V  V  <-> geti-
GRAM#5 [17] <100> na V  V  <-> nati-
GRAM#5 [18] <100>  V  V  V  <-> dhagena
GRAM#5 [19] <100>  V  V  V  <-> teenake
GRAM#5 [20] <100>  V  V  V  <-> dheenage
GRAM#5 [21] <100>  V  V  V  <-> dhatrkt
GRAM#5 [22] <100>  V  V  V  <-> trktdha
GRAM#5 [23] <100>  V  V  V  V  <-> tidhagena
GRAM#5 [24] <100>  V  V  V  V  <-> dhagedheena
GRAM#5 [25] <100>  V  V  V  V  <-> teena-ta
GRAM#5 [26] <100>  #ti  V  V  V  V  <->  #ti tidhatrkt
GRAM#5 [27] <100>  V  V  V  V  <-> dheenagena
GRAM#5 [28] <100>  V  V  V  V  <-> teenakena
GRAM#5 [29] <100>  V  V  V  V  V  <-> dhagenadheena
GRAM#5 [30] <100>  V  V  V  V  V  <-> dhagenateena
GRAM#5 [31] <100>  V  V  V  V  V  <-> dhagenadhati
GRAM#5 [32] <100>  V  V  V  V  V  <-> dhatrktdhati
GRAM#5 [33] <100>  V  V  V  V  V  <-> dhatidhatrkt
GRAM#5 [34] <100>  V  V  V  V  V  <-> dhatidhagena
GRAM#5 [35] <100>  V  V  V  V  V  V  <-> dheenagedhatrkt
GRAM#5 [36] <100>  V  V  V  V  V  V  <-> genagedhatrkt
GRAM#5 [37] <100>  V  V  V  V  V  V  <-> dhagedheenagena
GRAM#5 [38] <100>  V  V  V  V  V  V  <-> dhageteenakena
GRAM#5 [39] <100>  #ti  V  V  V  V  V  V  <->  #ti tidhagedheenage
GRAM#5 [40] <100>  V  V  V  V  V  V  <-> teenakegenage
GRAM#5 [41] <100>  V  V  V  V  V  V  V  V  <-> dhagenagenanagena
GRAM#5 [42] <100>  V  V  V  V  V  V  V  V  <-> dhatidhagedheenagena
GRAM#5 [43] <100>  V  V  V  V  V  V  V  V  <-> dhatidhageteenakena
GRAM#5 [44] <100>  V  V  V  V  V  V  V  V  A8  <-> dhatrktdhatidhatrkt A8 
GRAM#5 [45] <100>  V  V  V  V  V  V  V  V  A'8  <-> dhatrktdhatidhatrkt A'8 
-------------------------------------------------------------
ORD 
GRAM#6 [1]  LEFT + ? ? ? ? ? ? ? ? ? ? A'6-2  <-> + ? ? ? ? ? ? ? ? ? ? dhageteena
GRAM#6 [2]  LEFT + ? ? ? ? ? ? ? ? ? ? C6-2  <-> + ? ? ? ? ? ? ? ? ? ? dhagedheena
GRAM#6 [3]  LEFT + ? ? ? ? ? ? ? ? ? ? A'6-2  <-> + ? ? ? ? ? ? ? ? ? ? dhageteena
GRAM#6 [4]  LEFT + ? ? ? ? ? ? ? ? A8-2  <-> + ? ? ? ? ? ? ? ? dhatidhagedheena
GRAM#6 [5]  LEFT  A8-2  ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ; <-> dhatidhagedheena ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ;
GRAM#6 [6]  LEFT + ? ? ? ? ? ? ? ? A'8-2  <-> + ? ? ? ? ? ? ? ? dhatidhageteena
GRAM#6 [7]  LEFT + ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? A'8-2  <-> + ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? ? dhatidhageteena
GRAM#6 [8]  LEFT  A'6 + <-> dhageteenakena+
GRAM#6 [9]  LEFT  A'8 + <-> dhatidhageteenakena+
GRAM#6 [10]  LEFT  A4 + <-> dheenagena+
GRAM#6 [11]  LEFT  C6 + <-> dhagedheenagena+
GRAM#6 [12]  LEFT  A8 + <-> dhatidhagedheenagena+
GRAM#6 [13]  LEFT  A8 ; <-> dhatidhagedheenagena;
GRAM#6 [14]  LEFT + A16-4  <-> +dhatidhagenadhatrktdhatidhage
GRAM#6 [15]  LEFT + A16-2 <-> +dhatidhagenadhatrktdhatidhagedheena
GRAM#6 [16]  LEFT + A'16-2  #ke  <-> +dhatidhagenadhatrktdhatidhageteena #ke 
GRAM#6 [17]  LEFT + A16  <-> +dhatidhagenadhatrktdhatidhagedheenagena
GRAM#6 [18]  LEFT + A'16  <-> +dhatidhagenadhatrktdhatidhageteenakena
-------------------------------------------------------------
LIN
GRAM#7[1] Lahra -->  _transpose(12) do5_do5do5 do5_do5do5 si4_si4do5 la4_la4si4 lab4_lab4_ la4_si4mi5 do5_si4do5 la4_lab4la4
GRAM#7[2] Lahra --> _transpose(12) do5do5do5do5  si4la4si4do5 si4la4fa4{1,mi4do4} mi4fa4la4si4

// -- End of grammar