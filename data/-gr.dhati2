// Bol Processor version BP2.6.1
// Grammar file saved as '-gr.dhati2'. Date: Mer 26 Juin 1996 -- 11:36
-ho.dhati
-se.dhati2

// Grammar of "dhatidhagena" qa'ida commented in Kippen & Bel (1992)
// "Modelling Music with Grammars...", in Marsden & Pople (eds)
// "Computer Representations and Models in Music", Academic Press.

RND
GRAM#1[1] S <-> 4+4+4+4/4 (=A16)(=V8)(=A'8)*(:A16)(:V8)(=A8)
GRAM#1[2] S <-> 4+4+4+4/4 (=V16)(=A'16)*(:V16)(=A16)
GRAM#1[3] S <-> 4+4+4+4/4 (=V24)(=A'8)*(:V24)(=A8)
--------------------------------------------------
RND
GRAM#2[1] V8 <-> M V V V V V V V V
GRAM#2[2] V16 <-> M V V V V V V V V V V V V V V V V
GRAM#2[3] V24 <-> M V V V V V V V V V V V V V V V V V V V V V V V V
--------------------------------------------------
RND
GRAM#3[1] LEFT V <-> V1
GRAM#3[2] LEFT V V <-> trkt
GRAM#3[3] LEFT V V V <-> V3
--------------------------------------------------
RND
GRAM#4[1] #(M) ? ? M ) <-> ? ? )  [Ô#(M)Õ is remote negative context. Ô? ?Õ is needed to make sure that Ô)Õ is a not a slave parenthesis.  This rule must be first in subgrammar.]
GRAM#4[2] M V3 <-> dhagena M
GRAM#4[3] M trkt <-> trkt M
GRAM#4[4] #dha M V1 <-> #dha dha M
GRAM#4[5] #- M V1 <-> #- - M
GRAM#4[6] dha M V1 #tr <-> dha ti M #tr
GRAM#4[7] na M V1 #tr <-> na ti M #tr
GRAM#4[8] - M V1 #tr <-> - ti M #tr
GRAM#4[9] (= M V1 #tr <-> (= ti M #tr
--------------------------------------------------
ORD
GRAM#5[1] A8 <-> dhatidhagedheenagena
GRAM#5[2] A'8 <-> dhatidhageteenakena
GRAM#5[3] A16 <-> dhatidhagenadhatrktdhatidhagedheenagena
GRAM#5[4] A'16 <-> dhatidhagenadhatrktdhatidhageteenakena