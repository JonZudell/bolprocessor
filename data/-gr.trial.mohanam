// Bol Processor version BP2.9.2
// Grammar file saved as '-gr.trial.mohanam'. Date: Dim 6 Sep 1998 -- 13:59
-or.trial.mohanam
-ho.trial.mohanam
-se.trial.mohanam

// Composition by Kumar S. Subramanian, June 1995

// Remark (BB): this is an interesting way of using flags
// Also note the 'trn' homomorphism used for tonal pattern modifications

// Use a flute sound with a patch which is sensitive to velocity

RND
GRAM#1[1] S --> _transpose(-5) I /Ideas=20/ /Notes=32/ /NumR=1/

-------------------------
RND
GRAM#2[1] <0> I --> I /NumR+1/  [This rule is only fired on a _goto()]
GRAM#2[2] <20> /Ideas-1/ I --> R1 A R2
GRAM#2[3] <50> /Ideas-2/ I --> A B
GRAM#2[4] <20> /Ideas-1/ I --> R1 B R2
GRAM#2[5] <50> /Ideas-2/ I --> B A

-------------------------
RND
GRAM#3[1] <100> /Ideas/ /NumR-1/ A --> I 
GRAM#3[2] <100> /Ideas/ /NumR-1/ B --> I
GRAM#3[3] <1> /Ideas/ I --> I _goto(2,1)

------------------------
SUB1
GRAM#4[1] I --> lambda

------------------------
RND
GRAM#5[1] <50-12> /Notes-4/ A --> P4
GRAM#5[2] <50-9> /Notes-3/ A --> P3
GRAM#5[3] <50-10> /Notes-2/ A --> P2

GRAM#5[4] <50-12> /Notes-4/ B --> Q4
GRAM#5[5] <50-9> /Notes-3/ B --> Q3
GRAM#5[6] <50-10> /Notes-2/ B --> Q2

---------------------------
RND
GRAM#6[1] R1 ?1 R2 --> ?1 ?1
GRAM#6[2] R1 ?1 ?2 R2 --> ?1 ?2 ?1 ?2
GRAM#6[3] R1 ?1 ?2 ?3 R2 --> ?1 ?2 ?3 ?1 ?2 ?3
GRAM#6[4] R1 ?1 ?2 ?3 ?4 R2 --> ?1 ?2 ?3 ?4 ?1 ?2 ?3 ?4
GRAM#6[5] R1 ?1 ?2 ?3 ?4 ?5 R2 --> ?1 ?2 ?3 ?4 ?5 ?1 ?2 ?3 ?4 ?5

------------------------
RND
GRAM#7[1] P4 --> P41
GRAM#7[2] P4 --> P42
GRAM#7[3] P4 --> P43
GRAM#7[4] P4 --> P44

GRAM#7[5] P3 --> P31
GRAM#7[6] P3 --> P32

GRAM#7[7] P2 --> P21
GRAM#7[8] P2 --> P22

GRAM#7[9] Q4 --> Q41
GRAM#7[10] Q4 --> Q42
GRAM#7[11] Q4 --> Q43
GRAM#7[12] Q4 --> Q44

GRAM#7[13] Q3 --> Q31
GRAM#7[14] Q3 --> Q32
GRAM#7[15] Q3 --> Q33
GRAM#7[16] Q3 --> Q34

GRAM#7[17] Q2 --> Q21
GRAM#7[18] Q2 --> Q22
GRAM#7[19] Q2 --> Q23

---------------------------
RND
GRAM#8[1] R1 --> lambda
GRAM#8[2] R2 --> lambda

---------------------------
RND [Effects]
GRAM#9[1] Str ?1 --> _vel(110) ?1 _vel(64) 
GRAM#9[2] Step3Up ?1 ?2 ?3 --> _vel(80) ?1 _vel(95) ?2 _vel(110) ?3 _vel(64) 
GRAM#9[3] Step3Dn ?1 ?2 ?3 --> _vel(110) ?1 _vel(95) ?2 _vel(80) ?3 _vel(64)

GRAM#9[4] P41 --> sa6 re6 ga6 pa6
GRAM#9[5] P42 --> re6 ga6 pa6 ga6
GRAM#9[6] <0> P42 --> sa6 re6 ga6 re6
GRAM#9[7] P43 --> dha6 pa6 ga6 pa6
GRAM#9[8] <0> P43 --> ga6 re6 sa6 re6
GRAM#9[9] P44 --> ga6 Str dha6 pa6 Str dha6
GRAM#9[10] <0>P44 --> sa6 ga6 re6 ga6

GRAM#9[11] P31 --> ga6 pa6 dha6
GRAM#9[12] <0> P31 --> sa6 re6 ga6
GRAM#9[13] P32 --> sa6 ga6 re6
GRAM#9[14] P33 --> Str ga6 re6 sa6
GRAM#9[15] P34 --> Str sa7 dha6 pa6
GRAM#9[16] <0> P34 --> ga6 re6 sa6

GRAM#9[17] P21 --> ga6 pa6
GRAM#9[18] <0> P21 --> sa6 re6
GRAM#9[19] P22 --> sa6 Str ga6

GRAM#9[20] Q41 --> Str ga6 _ re6 sa6
GRAM#9[21] <0> Q41 --> sa6 _ re6 sa6
GRAM#9[22] Q42 --> Str re6 ga6 _ re6
GRAM#9[23] <0> Q42 --> sa6 re6 _ sa6
GRAM#9[24] Q43 --> ga6 _ pa6 Str dha6
GRAM#9[25] <0> Q43 --> sa6 _ re6 ga6
GRAM#9[26] Q44 --> Str sa6 re6 _ ga6

GRAM#9[27] <0> Q31 --> pa6 _ dha6
GRAM#9[28] Q31 --> sa6 _ re6
GRAM#9[29] Q32 --> sa6 _ ga6
GRAM#9[30] Q33 --> Str sa7 dha6
GRAM#9[31] <0> Q33 --> sa6 re6 _
GRAM#9[32] Q34 --> ga6 _ _
GRAM#9[33] <0> Q34 --> re6 _ sa6

GRAM#9[34] Q21 --> ga6 ga6
GRAM#9[35] <0> Q21 --> sa6 sa6	[ just temporary ]
GRAM#9[36] Q22 --> pa6 pa6
GRAM#9[37] <0> Q22 --> re6 re6
GRAM#9[38] Q23 --> Str dha6 Str dha6
GRAM#9[39] <0> Q23 --> ga6 ga6

// ---------------------------------------+