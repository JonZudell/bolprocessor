// Bol Processor version BP2.9.0
// Grammar file saved as '-gr.Visser6'. Date: Lun 15 Juin 1998 -- 1:00
// A piece by Harm Visser (April 1998)
-or.Visser6
-se.Visser6

ORD
_mm(60.0000) _striated
gram#1[1] S --> Part1 Part2 Part3

gram#1[2] Part1 -->  {_rndvel(10) _vel(80) Chord1 Chord2 Chord3 1 Chord1 Chord4 Chord5 Chord6, 3 Sax1 -}

gram#1[3] Part2 --> {/2 Sax1 __} {/3 Sax2} {/4 Sax3} {/5 Sax4} {/3 Sax2} {/4 Sax3} {/5 Sax4} {_transpose(1){/4 Sax2} {/5 Sax3} {/6 Sax4}, {M1}} {_transpose(1){/4 Sax2} {/5 Sax3} {/6 Sax4}, {M1}} - {_keyxpand(C5,-1){Chord1, Sax1} {_transpose(-1) /3 Chord1, Sax1} {_transpose(-2) /4 Chord1,Sax1}}

gram#1[4] Part3 --> {_keyxpand(F4,-1) /2 _rndvel(10) _vel(80) Chord1 Chord2 Chord3 - Chord1 Chord4 Chord5 Chord6, --- Sax1 -}

---
ORD
gram#2[1] Chord1 --> {M1 M2 - M1 M2 - M3}
gram#2[2] Chord2 --> {M1 M2 ¥ - M1 M2 - M3}
gram#2[3] Chord3 --> {M4 1 M3 M4}
gram#2[4] Chord4 --> {M5}
gram#2[5] Chord5 --> {_transpose(7) Chord4 Chord3 Chord2 Chord1}
gram#2[6] Chord6 --> M6

----
ORD
gram#3[1]  M1 -->  {C3, B3, A#4}
gram#3[2]  M2 -->  {_transpose(5) M1}
gram#3[3] M3 -->  {_keyxpand(C3,-1) {M1, M2}}
gram#3[4] M4 --> {_keymap(C3,C5,C5,C1) M3}
gram#3[5] M5 --> {_transpose(11) M4 M3 M2 M1}
gram#3[6] M6 --> {_keymap(C1,C5,C2,C1) M5}

---
ORD
gram#4[1] Sax1 --> {_chan(9) C4 B5 A#4}
gram#4[2] Sax2 --> {_keyxpand(C5,-1) Sax1}
gram#4[3] Sax3 --> {_keyxpand(B4,-1) Sax1}
gram#4[4] Sax4 --> {_keyxpand(A4,-1) Sax1}

COMMENT:
A study of chords making use of transpositions and key mapping
Rules gram#1[3] and gram#1[4] make use of explicit tempo markers, which the musician found convenient to indicate tempo changes.  This is not a good strategy with BP2 as it may lead to conflicts of duration during the expansion of the polymetric expression.  Such conflicts might even occurr in this example because the polymetric expression algorithm encountered an overflow and may not have checked the consistency of durations in absolute accuracy.
After discussing this matter with Harm, we decided to create a new tool for relative tempo changes.  Instead of writing "/5" (absolute) the musician will write "_tempo(5)" indicating a relative change in the sequence.