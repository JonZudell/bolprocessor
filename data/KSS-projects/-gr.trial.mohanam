V.2.5
Date: Sam 24 Juin 1995 -- 19:25
-ho.trial.mohanam
-se.trial.mohanam 

// Work in progress by Kumar S. Subramanian, June 1995

// Remark (BB): this is an interesting way of using flags
// Also note the 'trn' homomorphism used for tonal pattern modifications

INIT: Play _script(MIDI program 10 [basic channel])

RND
GRAM#1[1] S --> _transpose(-5) /4 I /Ideas=20/ /Notes=32/ /NumR=1/

GRAM#1[2] <0> I --> I /NumR+1/  [This rule is only fired on a _goto()]
GRAM#1[3] <20> /Ideas-1/ I --> R1 A R2
[GRAM#1[4] <20> /Ideas-1/ I --> T1 A T2]
GRAM#1[4] <50> /Ideas-2/ I --> A B
GRAM#1[5] <20> /Ideas-1/ I --> R1 B R2
[GRAM#1[7] /Ideas-1/ I --> T1 B T2]
GRAM#1[6] <50> /Ideas-2/ I --> B A


-------------------------

RND
GRAM#2[1] <100> /Ideas/ /NumR-1/ A --> I 
GRAM#2[2] <100> /Ideas/ /NumR-1/ B --> I
GRAM#2[3] <1> /Ideas/ I --> I _goto(1,2)

------------------------

SUB1
GRAM#3[1] I -->

------------------------

RND
GRAM#4[1] <50-12> /Notes-4/ A --> P4
GRAM#4[2] <50-9> /Notes-3/ A --> P3
GRAM#4[3] <50-10> /Notes-2/ A --> P2

GRAM#4[4] <50-12> /Notes-4/ B --> Q4
GRAM#4[5] <50-9> /Notes-3/ B --> Q3
GRAM#4[6] <50-10> /Notes-2/ B --> Q2

---------------------------

RND
GRAM#5[1] R1 ?1 R2 --> ?1 ?1
GRAM#5[2] R1 ?1 ?2 R2 --> ?1 ?2 ?1 ?2
GRAM#5[3] R1 ?1 ?2 ?3 R2 --> ?1 ?2 ?3 ?1 ?2 ?3
GRAM#5[4] R1 ?1 ?2 ?3 ?4 R2 --> ?1 ?2 ?3 ?4 ?1 ?2 ?3 ?4
GRAM#5[5] R1 ?1 ?2 ?3 ?4 ?5 R2 --> ?1 ?2 ?3 ?4 ?5 ?1 ?2 ?3 ?4 ?5

/*
GRAM#5[6] T1 ?1 T2 --> ?1 trn(=?1)
GRAM#5[7] T1 ?1 ?2 T2 --> ?1 ?2 trn(=?1 ?2)
GRAM#5[8] T1 ?1 ?2 ?3 T2 --> ?1 ?2 ?3 trn(=?1 ?2 ?3)
GRAM#5[9] T1 ?1 ?2 ?3 ?4 T2 --> ?1 ?2 ?3 ?4 trn(=?1 ?2 ?3 ?4)
GRAM#5[10] */

------------------------

RND
GRAM#6[1] P4 --> P41
GRAM#6[2] P4 --> P42
GRAM#6[3] P4 --> P43
GRAM#6[4] P4 --> P44

GRAM#6[5] P3 --> P31
GRAM#6[6] P3 --> P32

GRAM#6[7] P2 --> P21
GRAM#6[8] P2 --> P22


GRAM#6[9] Q4 --> Q41
GRAM#6[10] Q4 --> Q42
GRAM#6[11] Q4 --> Q43
GRAM#6[12] Q4 --> Q44

GRAM#6[13] Q3 --> Q31
GRAM#6[14] Q3 --> Q32
GRAM#6[15] Q3 --> Q33
GRAM#6[16] Q3 --> Q34

GRAM#6[17] Q2 --> Q21
GRAM#6[18] Q2 --> Q22
GRAM#6[19] Q2 --> Q23

---------------------------
RND
GRAM#7[1] R1 -->
GRAM#7[2] R2 -->

/*
GRAM#7[3] T1 -->
GRAM#7[4] T2 -->
GRAM#7[5] */
---------------------------

RND [Effects]
GRAM#8[1] Str ?1 --> _vel(110) ?1 _vel(64) 
GRAM#8[2] Step3Up ?1 ?2 ?3 --> _vel(80) ?1 _vel(95) ?2 _vel(110) ?3 _vel(64) 
GRAM#8[3] Step3Dn ?1 ?2 ?3 --> _vel(110) ?1 _vel(95) ?2 _vel(80) ?3 _vel(64)

GRAM#8[4] P41 --> sa6 re6 ga6 pa6
GRAM#8[5] P42 --> re6 ga6 pa6 ga6
GRAM#8[6] <0> P42 --> sa6 re6 ga6 re6
GRAM#8[7] P43 --> dha6 pa6 ga6 pa6
GRAM#8[8] <0> P43 --> ga6 re6 sa6 re6
GRAM#8[9] P44 --> ga6 Str dha6 pa6 Str dha6
GRAM#8[10] <0>P44 --> sa6 ga6 re6 ga6

GRAM#8[11] P31 --> ga6 pa6 dha6
GRAM#8[12] <0> P31 --> sa6 re6 ga6
GRAM#8[13] P32 --> sa6 ga6 re6
GRAM#8[14] P33 --> Str ga6 re6 sa6
GRAM#8[15] P34 --> Str sa7 dha6 pa6
GRAM#8[16] <0> P34 --> ga6 re6 sa6

GRAM#8[17] P21 --> ga6 pa6
GRAM#8[18] <0> P21 --> sa6 re6
GRAM#8[19] P22 --> sa6 Str ga6

GRAM#8[20] Q41 --> Str ga6 _ re6 sa6
GRAM#8[21] <0> Q41 --> sa6 _ re6 sa6
GRAM#8[22] Q42 --> Str re6 ga6 _ re6
GRAM#8[23] <0> Q42 --> sa6 re6 _ sa6
GRAM#8[24] Q43 --> ga6 _ pa6 Str dha6
GRAM#8[25] <0> Q43 --> sa6 _ re6 ga6
GRAM#8[26] Q44 --> Str sa6 re6 _ ga6

GRAM#8[27] <0> Q31 --> pa6 _ dha6
GRAM#8[28] Q31 --> sa6 _ re6
GRAM#8[29] Q32 --> sa6 _ ga6
GRAM#8[30] Q33 --> Str sa7 dha6
GRAM#8[31] <0> Q33 --> sa6 re6 _
GRAM#8[32] Q34 --> ga6 _ _
GRAM#8[33] <0> Q34 --> re6 _ sa6

GRAM#8[34] Q21 --> ga6 ga6
GRAM#8[35] <0> Q21 --> sa6 sa6	[ just temporary ]
GRAM#8[36] Q22 --> pa6 pa6
GRAM#8[37] <0> Q22 --> re6 re6
GRAM#8[38] Q23 --> Str dha6 Str dha6
GRAM#8[39] <0> Q23 --> ga6 ga6

// ---------------------------------------+