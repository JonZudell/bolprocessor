// Bol Processor version BP2.7.4
// Grammar file saved as '-gr.Montaudon4'. Date: Ven 5 dŽc 1997 -- 23:19
// Grammar composed by  T. Montaudon (buil@club-internet.fr)

//Kill WolfE with a Bic Pen Pea-shooter//

-se.Montaudon4
-or.Montaudon4

// ch.01 GM-a 047Harp
// ch.02 GM-a 047Harp
// ch.03 GM-a 047Harp
// ch.04 GM-a 047Harp
// ch.05 GM-a 115SteelDrums
// ch.06 GM-a 115SteelDrums
// ch.06 GM-a 115SteelDrums
// ch.07 GM-a 122BreathNoiz
// light reverb. sound not too loud

RND

gram#1[1] S --> {W O O O O O O O, L} 
gram#1[2] O --> F F E E E E E E E

-------

RND

gram#2[1] W --> _chan(4) _vel(55) _volume(75) _pan(90)  {3, {C4 B4 D3}.{D3 C4 B4} {B4 D3 C4}} _chan(1) _pan(50) {3, {C4 B4 D3}. { D3 C4 B4} {B4 D3 C4}}

gram#2[2] F --> {_chan(4) _pan(100) _vel(55) _volume(70) {3, V - V - V - V - V - V - V - V - V -}, _chan(2) _pan(60) _vel(50) _volume(60) {3, - V - V - V - V - V - V - V - V - V}}

gram#2[3] E --> _vel(80) {3,{_volumecont_volume(70) _legato(100) _chan(1) _pan(40) T T T T T T T T T}, {X}}

gram#2[4] T --> C4
gram#2[5] T --> B4
gram#2[6] T --> D3
gram#2[7] T --> E3
gram#2[8] T --> F3
gram#2[9] T --> F#3
gram#2[10] T --> E#4
gram#2[11] T --> A#4
gram#2[12] T --> -

gram#2[13] <80>X --> _pan(80) {3, K K K K K K K K K K K K K K K K K K}
gram#2[14] <20>X --> _pan(50) _chan(3) {3, V V V V V V V V V V V V V V V V V V}
gram#2[15] <10>X --> F

gram#2[16] L --> {195, 45 R 20 R 15 R 10 R 10 R R 5 R 5 R 5, 80 Z 7 Z 23 Z 20 Z 5}
gram#2[17] R --> _vel(100) _chan(5) _pan(95) {_volumecont_volume(80) A3_________ _volume(30)}
gram#2[18] Z --> _vel(100) _chan(6) _pan(45) {_volumecont_volume(80) A4______________ _volume(30)}

-------

LIN

gram#3[1] <80>V --> C4
gram#3[2] <80>#B4 V --> #B4 B4
gram#3[3] <80>V  --> D3

gram#3[4] <40>K --> _chan(7) C4
gram#3[5] <40>#B4 K --> _chan(7) #B4 B4
gram#3[6] <40>K  --> _chan(7) D3
gram#3[7] <60>K --> _chan(2) C4
gram#3[8] <60>#B4 K --> _chan(2) #B4 B4
gram#3[9] <60>K  --> _chan(2) D3