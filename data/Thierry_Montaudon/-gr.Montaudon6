// Bol Processor version BP2.8.1
// Grammar file saved as '-gr.Montaudon6'. Date: Mar 14 Avr 1998 -- 14:23
// From: Thierry Montaudon <buil@club-internet.fr>

// Work in progress

-se.Montaudon6
-or.string

// Korg synthesizer. Channels will be programmed automatically thanks to the
// Ô-or.stringÕ MIDI orchestra.
// channel 1: 044 contrabass
// channel 2: 072 clarinet
// channel 3: 001 piano

ORD

gram#1[1] S --> X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X X Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Y Z Z Z Z Z Z Z Z Z Z Z Z Z Z Z Z1 Z1 Z1 Z1 Z1 Z2 Z1 Z2 Z1 Z2 Z3 Z2 Z3 Z2 Z3 Z4 Z3 Z4 Z1 Z3 Z2 Z3 Z4 Z4 Z4

gram#1[2] X --> {3, A, B}
gram#1[3] Y --> {3, A, E}
gram#1[4] Z --> {3, A", E}
gram#1[5] Z1 --> {3, A", E, F}
gram#1[6] Z2 --> {3, A", E, G}
gram#1[7] Z3 --> {3, A", E, H}
gram#1[8] Z4 --> {3, A", E, I}  

-------
RND
gram#2[1] A --> A'
gram#2[2] A --> _transpose(-1) A'
gram#2[3] A --> _transpose(-2) A'

gram#2[4] A' --> {C - C - C - C -}
gram#2[5] A' --> {C {- C - C - C -, - T A2__ 3}}
gram#2[6] B --> {- D - D - D - D}
gram#2[7] B --> {- D - {D - D - D, M E2____}}

gram#2[8] E --> B
gram#2[9] E --> _transpose(-2) B
gram#2[10] E --> _transpose(-4) B

gram#2[11] A" --> {C - C' - C - C' -}
gram#2[12] A" --> {C {- C' - C - C' -, - T A2__ 3}} 

gram#2[13] C --> _chan(1) T A3
gram#2[14] D --> _chan(2) M E4
gram#2[15] C' --> _transpose(7) C

gram#2[16] F --> _chan(3) _vel(20) _legato(75) {_volume(90) - - {A4, C4, E4} - - {A4, C4, E4} - -}
gram#2[17] G --> _chan(3) _vel(20) _legato(75) {_volume(90) - - {A4, C4, E4} - A4 {A4, C4, E4} - -}
gram#2[18] H --> _chan(3) _vel(20) _legato(75) {_volume(90) - - {A4, C4, E4} - - {A5, C5, E5} - -}
gram#2[19] I --> _chan(3) _vel(20) _legato(75) {_volume(90) - - {B4, D4, F4} - - {B4, D4, F4} - -}

-------
RND
gram#3[1] T --> _vel(55) _volume(75)
gram#3[2] T --> _vel(60) _volume(75)
gram#3[3] T --> _vel(65) _volume(75)

gram#3[4] M --> _vel(65) _volume(55)
gram#3[5] M --> _vel(70) _volume(55) 
gram#3[6] M --> _vel(75) _volume(55)

COMMENT:
This is a work in progress.  You must play it on a Korg synth to appreciate the sound!  I am providing it just for new users to get familiar with various
composition techniques that can be used with BP2.